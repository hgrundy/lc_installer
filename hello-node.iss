; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Hello Node"
#define MyAppShortName "HelloNode"
#define MyAppLCShortName "hellonode"
#define MyAppVersion "1.0"
#define MyAppPublisher "Hurp Durp, LLC"
#define MyAppURL "http://hurpdurp.com/"
#define MyAppExeName "winstart-browser.bat"

#define MyAppIcon "web-trifecta-badge.ico"

#define NSSM "nssm.exe"
#define NSSM32 "nssm-x86.exe"
#define NSSM64 "nssm.exe"
#define NODE32 "node-v0.10.4-x86.msi"
#define NODE64 "node-v0.10.4-x64.msi"
#define NODE "node-v0.10.4-x64.msi"
#define USERPROFILE "C:\Users\coolaj86"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{97DA02F5-2E8C-4B96-BB42-61ED2BBF34DF}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppShortName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
;LicenseFile={#USERPROFILE}\Downloads\LICENSE.txt
;InfoBeforeFile={#USERPROFILE}\Downloads\before.txt
;InfoAfterFile={#USERPROFILE}\Downloads\after.txt
OutputDir={#USERPROFILE}\Desktop
OutputBaseFilename={#MyAppShortName}Installer
SetupIconFile={#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\{#MyAppIcon}
Compression=lzma
SolidCompression=yes


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"


[Files]
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\winstart-server.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\winstart-browser.bat"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\msie-app.hta"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\msie-app-secure.hta"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\{#NODE64}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\{#NSSM64}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\{#NSSM32}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\winstaller\{#MyAppIcon}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#USERPROFILE}\Downloads\{#MyAppShortName}-master\{#MyAppShortName}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files


[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\{#MyAppIcon}"

; Here's an example of how you could use a start menu item for just Chrome, no batch file
;Name: "{group}\{#MyAppName}"; Filename: "{pf32}\Google\Chrome\Application\chrome.exe"; Parameters: "--app=http://localhost:5566 --user-data-dir=%APPDATA%\{#MyAppShortName}\"; IconFilename: "{app}\{#MyAppIcon}"


[Run]
; postinstall launch
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent runhidden

; These all run with 'runascurrentuser' (i.e. admin) whereas 'runasoriginaluser' would refer to the logged in user
; Install Node
Filename: "{sys}\msiexec.exe"; Parameters: "/passive /i ""{app}\{#NODE}""";

; Add Firewall Rules
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Node In"" program=""{pf64}\nodejs\node.exe"" dir=in action=allow enable=yes"; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Node Out"" program=""{pf64}\nodejs\node.exe"" dir=out action=allow enable=yes"; Flags: runhidden;

; Add System Service
Filename: "{app}\{#NSSM}"; Parameters: "install {#MyAppShortName} ""{pf64}\nodejs\node.exe"" ""{app}\bin\server.js"" ""5566"""; Flags: runhidden;
Filename: "{sys}\net.exe"; Parameters: "start {#MyAppShortName}"; Flags: runhidden;
;Filename: "{pf64}\nodejs\node.exe"; Parameters: "{app}\bin\windows-service-installer.js install"; Flags: runhidden;
;Filename: "{sys}\net.exe"; Parameters: "start {#MyAppLCShortName}"; Flags: runhidden;


[UninstallRun]
; Removes System Service
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppShortName}"; Flags: runhidden;
Filename: "{app}\{#NSSM}"; Parameters: "remove {#MyAppShortName} confirm"; Flags: runhidden;
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppLCShortName}"; Flags: runhidden;
Filename: "{pf64}\nodejs\node.exe"; Parameters: "{app}\bin\windows-service-installer.js remove"; Flags: runhidden;

; Remove Firewall Rules
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node In"" program=""{pf64}\nodejs\node.exe"""; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node Out"" program=""{pf64}\nodejs\node.exe"""; Flags: runhidden;

; Uninstall Node
Filename: "{sys}\msiexec.exe"; Parameters: "/passive /x ""{app}\{#NODE}""";

; Remove all leftovers
Filename: "{sys}\rmdir"; Parameters: "-r ""{app}""";
