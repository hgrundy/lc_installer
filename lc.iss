; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "LogicalCat"
;#define MyAppNameLower "logicalcat"
#define MyAppVersion "3.0"
#define MyAppPublisher "LogicalCat LLC"
#define MyAppURL "http://www.logicalcat.com/"
#define MyAppExeName "winstart-browser.bat"
#define MyAppIcon "square_logo.ico"

#define NSSM "nssm64.exe"
#define JAVA "jre-7u45-windows-x64.exe"
#define NODE "node-v0.10.21-x64.msi"
#define USERPROFILE "C:\Users\r-b-h"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2752ABBC-503D-4F3E-AC37-95EF35E343E3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=c:\logicalcat
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=C:\dev\lc_installer\out
OutputBaseFilename=setup
SetupIconFile="C:\dev\lc_installer\lib\square_logo.ico"
;Password=kitty
Compression=lzma
SolidCompression=yes
ChangesEnvironment=yes
ArchitecturesInstallIn64BitMode=x64





;  result := RegQueryStringValue(HKEY_LOCAL_MACHINE_64, 'SOFTWARE\JavaSoft\Java Runtime Environment\CurrentVersion');


[Code]
function JREExists(): Boolean;
begin
  Result := FileExists(ExpandConstant('{pf64}\Java\jre7\bin\java.exe'));
end;


[Types]
Name: "full";   Description: "Full: Browser App and all LogicalCat Crawlers"
Name: "demo";   Description: "Demo: Browser App and Business Documents"
Name: "custom"; Description: "Custom: Select Individual Crawlers";                  Flags: iscustom


[Components]
Name: "base";   Description: "Browser Application";        Types: full demo custom; Flags: fixed
Name: "dox";    Description: "Common Business Documents";  Types: full demo       
Name: "epf";    Description: "Common E&P Files:";          Types: full        
Name: "pet";    Description: "IHS Petra Projects";         Types: full
Name: "tks";    Description: "IHS Kingdom Suite Projects"; Types: full
Name: "ggx";    Description: "LMKR GeoGraphix Discovery";  Types: full          


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"


[Files]
Source: "C:\dev\lc_installer\lib\node-v0.10.21-x64.msi";    DestDir: "{app}\install";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\jre-7u45-windows-x64.exe"; DestDir: "{app}\install";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\rm_java.bat";              DestDir: "{app}\install";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\set_java_home.bat";        DestDir: "{app}\install";               Flags: ignoreversion
;Source: "C:\dev\lc_installer\lib\msie-app.hta";             DestDir: "{app}";               Flags: ignoreversion
;Source: "C:\dev\lc_installer\lib\msie-app-secure.hta";      DestDir: "{app}";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\winstart-browser.bat";     DestDir: "{app}\install";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\winstart-server.bat";      DestDir: "{app}\install";               Flags: ignoreversion
Source: "C:\dev\lc_installer\lib\nssm64.exe";               DestDir: "{app}";                Flags: ignoreversion

Source: "C:\dev\lc_installer\lib\square_logo.ico";          DestDir: "{app}\install";        Flags: ignoreversion
Source: "C:\dev\HelloNode\*";                               DestDir: "{app}\lc_browser_app"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\dev\lc_installer\lib\elasticsearch-0.90.x\*";   DestDir: "{app}\elasticsearch";  Flags: ignoreversion recursesubdirs createallsubdirs


[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\install\{#MyAppIcon}"

[Registry]
; set JAVA_HOME
; totally ghetto: setting var works here, but is apparently not readable by elasticsearch later on--despite invoking as current (admin) user
; as a workaround, I hard coded the path in elasticsearch\bin\service.bat to look for c:\program files\java\jre7
Root: HKLM64; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueName: "JAVA_HOME"; ValueType: String; ValueData: "{pf64}\java\jre7";

; set app stuff
;Root: HKLM64; Subkey: "Software\{#MyAppPublisher}"; Flags: uninsdeletekeyifempty
;Root: HKLM64; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}"; Flags: uninsdeletekey
;Root: HKLM64; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "Path"; ValueData: "{app}"
;Root: HKLM64; Subkey: "Software\{#MyAppPublisher}\{#MyAppName}\Settings"; ValueType: string; ValueName: "Version"; ValueData: "{#MyAppVersion}"


[Run]
; postinstall launch                                                
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent runhidden

; Install Node
Filename: "{sys}\msiexec.exe"; Parameters: "/qb INSTALLDIR=c:\logicalcat\nodejs /i ""{app}\install\{#NODE}"""; StatusMsg: "Installing Node.js..."

; Install Java (just keep default location)
Filename: "{app}\install\{#JAVA}"; Parameters: " /s "; StatusMsg: "Installing JRE7..."; Check: Not JREExists();

; Add Firewall Rules
Filename: "{sys}\netsh.exe";   Parameters: "advfirewall firewall add rule name=""Node In""  program=""{app}\nodejs\node.exe"" dir=in  action=allow enable=yes"; Flags: runhidden; StatusMsg: "Installing Firewall Rules..."
Filename: "{sys}\netsh.exe";   Parameters: "advfirewall firewall add rule name=""Node Out"" program=""{app}\nodejs\node.exe"" dir=out action=allow enable=yes"; Flags: runhidden; StatusMsg: "Installing Firewall Rules..."

; Add System Service
Filename: "{app}\{#NSSM}";     Parameters: "install {#MyAppName} ""{app}\nodejs\node.exe"" ""{app}\lc_browser_app\bin\server.js"" ""5566"""; Flags: runhidden; StatusMsg: "Installing LogicalCat Service..."
Filename: "{sys}\net.exe";     Parameters: "start {#MyAppName}"; Flags: runhidden; StatusMsg: "Starting LogicalCat Service..."

; Configure ElasticSearch service
Filename: "{app}\elasticsearch\bin\service.bat"; Parameters: "install"; Flags: shellexec waituntilterminated runhidden; StatusMsg: "Installing ElasticSearch Service..."
Filename: "{app}\elasticsearch\bin\service.bat"; Parameters: "start";   Flags: shellexec waituntilterminated runhidden; StatusMsg: "Starting ElasticSearch Service..."







;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

[UninstallRun]
; Remove System Service
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppName}"; Flags: runhidden;
Filename: "{app}\{#NSSM}"; Parameters: "remove {#MyAppName} confirm"; Flags: runhidden;
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppName}"; Flags: runhidden;
Filename: "{app}\nodejs\node.exe"; Parameters: "{app}\bin\windows-service-installer.js remove"; Flags: runhidden;


; Remove Firewall Rules
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node In"" program=""{app}\nodejs\node.exe""";  Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node Out"" program=""{app}\nodejs\node.exe"""; Flags: runhidden;


; Stop and Uninstall ElasticSearch Service
Filename: "{app}\elasticsearch\bin\service.bat"; Parameters: "stop";   Flags: shellexec waituntilterminated runhidden
Filename: "{app}\elasticsearch\bin\service.bat"; Parameters: "remove"; Flags: shellexec waituntilterminated runhidden

; Uninstall Node
Filename: "{sys}\msiexec.exe"; Parameters: "/passive /x ""{app}\{#NODE}""";


; Uninstall Java (could not use msiexec x{program id} ~ weird bracket escape in this horrible editor?)
; ...don't uninstall
;Filename: "{app}\rm_java.bat"; Flags: runhidden;

; Remove all leftovers
Filename: "{sys}\rmdir"; Parameters: "-r ""{app}""";


